*** Settings ***
Resource          BusinessKeywords.txt
Resource          LogicKeywords.txt
Resource          CommonKeywords.txt

*** Keywords ***
fsweb_Load Data From File
    [Arguments]    ${v_pathToData}    ${v_sheetName}
    [Documentation]    Author: Minh.nguyen
    ...
    ...    Desc: Load data from file (just select the first line)
    ...
    ...    Datetime: March 23, 2015
    ${v_data}=    Get Data By Sheet Name    ${v_pathToData}    ${v_sheetName}
    ${v_numberOfItems}=    Evaluate    len(${v_data})
    ${v_data}=    Run Keyword If    ${v_numberOfItems}==0    Create List    ELSE    Set Variable    ${v_data[0]}
    Set Suite Variable    ${v_data}
    Return From Keyword    ${v_data}

fsweb_Load Data From File - Data Driven
    [Arguments]    ${v_pathToData}    ${v_sheetName}
    [Documentation]    Author: Minh.nguyen
    ...
    ...    Desc: Load data from file (used for data driven) (load all data on that sheet)
    ...
    ...    Datetime: March 23, 2015
    ${v_data}=    Get Data By Sheet Name    ${v_pathToData}    ${v_sheetName}
    Set Suite Variable    ${v_data}
    Return From Keyword    ${v_data}

fsweb_Submit Test Status Into Testlink
    [Arguments]    ${v_testcaseID}
    [Documentation]    Author: Minh.nguyen
    ...
    ...    Desc: Submit test cases result into testlink
    ...
    ...    Datetime: March 23, 2015
    Run Keyword If    ${cfg_isSubmitToTestlink}==True    Submit Test Result    ${v_testcaseID}    ${TEST_STATUS}    ${TEST_MESSAGE}

fsweb_Revoke Recipient
    [Arguments]    ${v_recipientName}
    fsweb_Right Click    ${ViewRecipients_RecipientsTable}//span[@title="${v_recipientName}"]
    Click    ${MenuWhenRightClickOnARecipient_Revoke}
    Click    ${RevokeAccess_RevokeButton}
    Click    ${RevokeMessage_CloseButton}

fsweb_Right Click
    [Arguments]    ${v_elementLocator}
    Execute Javascript    var ele = document.evaluate('${v_elementLocator}',document,null,XPathResult.FIRST_ORDERED_NODE_TYPE,null).singleNodeValue;contextMenuClick(ele);function contextMenuClick(element){var evt = element.ownerDocument.createEvent('MouseEvents');var RIGHT_CLICK_BUTTON_CODE=2;evt.initMouseEvent('contextmenu',true,true,element.ownerDocument.defaultView,1,0,0,0,0,false,false,false,false,RIGHT_CLICK_BUTTON_CODE,null);if(document.createEventObject){return element.fireEvent('onclick',evt)}else{return !element.dispatchEvent(evt);}}

fsweb_Open A File Or Folder
    [Arguments]    ${v_fileNameOrFolderName}=FileString Received Files
    Wait Until Page Contains Element    //table[@id="list-file"]/tbody/tr/td/a[text()="${v_fileNameOrFolderName}"]    ${cfg_timeout}    Your file or folder "${v_fileNameOrFolderName}" doesn't exist
    Double Click Element    //table[@id="list-file"]/tbody/tr/td/a[text()="${v_fileNameOrFolderName}"]
    Wait For Progress Bar
