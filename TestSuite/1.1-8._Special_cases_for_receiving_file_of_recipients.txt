*** Settings ***
Suite Setup       Get Test Data    1.1-8. Special cased for receiv
Resource          ../Resource/BusinessKeywords.txt
Resource          ../Resource/LogicKeywords.txt
Library           ${CURDIR}/../Tool/Libs/DeleteUserInDatabase.py

*** Test Cases ***
String file to un-verified secondary email of owner but he will not receive this file when he verified it
    ${v_test_data}=    Set Variable    ${test_data[0]}
    Connect To Email    ${v_test_data['Owner']}    ${v_test_data['Email Pass']}
    Open Web App    browser1
    SignIn    ${v_test_data['Owner']}    ${v_test_data['Owner Pass']}
    Delete All Email
    Add 2nd Email    ${v_test_data['Direct Recipient']}
    Wait For Email
    ${v_verification_link}=    Get A Link In Email    verification
    ${v_pathToDataFile}=    Join Path    ${cfg_test_data_location}    FileTypeSupported    ${v_test_data['File']}
    String File    ${v_pathToDataFile}    ${v_test_data['Direct Recipient']}    ${v_test_data['Access Level']}    ${v_test_data['Expiration']}    ${v_test_data['Allow Printing']}    ${v_test_data['Display Watermark']}
    ...    ${v_test_data['Note']}
    Wait For Progress Bar
    #Verify 2nd email
    Wrapper.Open Browser    ${v_verification_link}    ${cfg_webBrowser}    browser2
    Verify Success Alert With Message    ${v_test_data['Expected Message']} ${v_test_data['Direct Recipient']}
    SignIn    ${v_test_data['Direct Recipient']}    ${v_test_data['Direct Recipient Pass']}
    ${v_recipients}=    Get Recipients    ${v_test_data['File']}    active
    Should Be Empty    ${v_recipients}    Recipient list should be empty
    [Teardown]    Run Keywords    Submit Test Result    ${v_test_data['TestcaseID']}    ${TEST_STATUS}    AND    Close Connection
    ...    AND    Delete File    ${v_test_data['File']}    AND    Delete 2nd Email    ${v_test_data['Direct Recipient']}
    ...    AND    Close All Browsers

String email to unverified secondary email and then it is created as primary email
    ${v_test_data}=    Set Variable    ${test_data[2]}
    Connect To Email    ${v_test_data['Owner']}    ${v_test_data['Email Pass']}
    Open Web App    browser1
    SignIn    ${v_test_data['Owner']}    ${v_test_data['Owner Pass']}
    Add 2nd Email    ${v_test_data['Direct Recipient']}
    ${v_pathToDataFile}=    Join Path    ${cfg_test_data_location}    FileTypeSupported    ${v_test_data['File']}
    String File    ${v_pathToDataFile}    ${v_test_data['Direct Recipient']}
    Wait For Progress Bar
    Wait For Email
    #Register recipient as primary
    Open Web App    browser2
    ${v_signup_info}=    Evaluate    {'First Name':'abcf', 'Last Name':'last hihi', 'Email':'${v_test_data['Direct Recipient']}', 'Password':'${v_test_data['Direct Recipient Pass']}'}
    Click    ${Signin_Link_SignUp}
    Wait Until Element Is Visible    ${Signup_Btn_Signup}
    Delete All Email
    SignUp    ${v_signup_info}
    Wait For Email
    ${v_verification_link}=    Get A Link In Email    activation
    #Verify email then check received file
    Wrapper.Open Browser    ${v_verification_link}
    Verify Success Alert With Message    ${v_test_data['Expected Message']}
    SignIn    ${v_test_data['Direct Recipient']}    ${v_test_data['Direct Recipient Pass']}
    Navigate To Folder    ${v_test_data['Received Folder']}
    Check File Exist    ${v_test_data['Received File']}
    [Teardown]    Run Keywords    Submit Test Result    ${v_test_data['TestcaseID']}    ${TEST_STATUS}    AND    Close Connection
    ...    AND    Switch Browser    browser1    AND    Delete File    ${v_test_data['File']}
    ...    AND    Delete User In Database    ${v_test_data['Direct Recipient']}    AND    Close All Browsers
